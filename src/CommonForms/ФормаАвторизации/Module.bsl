#Область ОписаниеПеременных

&НаКлиенте
Перем НомерУстройства, СоединениеИПараметры;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "СканШтрихкода" И НомерУстройства = Источник И ВводДоступен() Тогда
		ШтрихкодПоиск = Параметр;
		Пользователь = НайтиПользователяСервер(Параметр);
		КомандаВойти(Неопределено);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	ВходВозможен = Ложь;
	Если КомпонентыДляСчитыванияШтрихкодов <> Неопределено Тогда
		КомпонентыДляСчитыванияШтрихкодов.Подключить(НомерУстройства);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если КомпонентыДляСчитыванияШтрихкодов <> Неопределено Тогда
		КомпонентыДляСчитыванияШтрихкодов.Отключить(НомерУстройства);
	КонецЕсли;
	СоединениеИПараметры = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ ВходВозможен Тогда
		ЗавершитьРаботуСистемы(Ложь, Ложь);		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШтрихкодПоискПриИзменении(Элемент)
	Пользователь = НайтиПользователяСервер(ШтрихкодПоиск);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВойти(Команда)
	Если ВРег(ШтрихкодПоиск) = "СФ_АДМИН" Тогда
		ВходВозможен = Истина;
		Закрыть();
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан пользователь для входа!";
		Сообщение.ИдентификаторНазначения = ЭтаФорма.УникальныйИдентификатор;
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если УстановитьНастройкиПоПользователю(Пользователь) Тогда
		Если ЗначениеЗаполнено(ИмяФормыОткрыть) Тогда
			ОткрытьФорму(ИмяФормыОткрыть);
		КонецЕсли;
		ВходВозможен = Истина;
		
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НайтиПользователяСервер(Штрихкод)
	Возврат Штрихкодирование.НайтиДанныеПоШтрихкодуПользователь(Штрихкод);		
КонецФункции

&НаСервере
Функция УстановитьНастройкиПоПользователю(ТекущийПользователь)
	
	ПараметрыСеанса.ТекущийПользователь = ТекущийПользователь;
	
	Если ПараметрыСеанса.ТекущийПользователь.ОткрыватьРаботаСТСД Тогда
		ИмяФормыОткрыть = "Обработка.РаботаСТСД.Форма.ФормаСтарт";		
	КонецЕсли;
		
	Возврат Истина;		
КонецФункции

#КонецОбласти


