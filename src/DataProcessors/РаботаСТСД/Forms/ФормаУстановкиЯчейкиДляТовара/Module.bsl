#Область ОписаниеПеременных

&НаКлиенте
Перем НомерУстройства, СоединениеИПараметры;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СканШтрихкода" И НомерУстройства = Источник И ВводДоступен() Тогда

		ШтрихкодПоиск = Параметр;

		ОбработатьШтрихкод(Параметр); //процедура для обработки ШК

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	Если КомпонентыДляСчитыванияШтрихкодов <> Неопределено Тогда
		КомпонентыДляСчитыванияШтрихкодов.Подключить(НомерУстройства);
	КонецЕсли;
	
	НастройкиСоединения = ПолучитьНастройкиСоединения();
	СоединениеИПараметры = ОбменДаннымиКлиентСервер.ПолучитьСоединениеИПараметры(НастройкиСоединения);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если КомпонентыДляСчитыванияШтрихкодов <> Неопределено Тогда
	
		КомпонентыДляСчитыванияШтрихкодов.Отключить(НомерУстройства);
	
	КонецЕсли;
	
	СоединениеИПараметры = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШтрихкодПриИзменении(Элемент)
	ОбработатьШтрихкод(ШтрихкодПоиск);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьДанныеПоШтрихкоду(ШтрихкодаСтрока)
	
	АдресРесурсаСПараметрами = СоединениеИПараметры.Параметры.АдресРесурса + "/infoOnBarcode?barcode=" + СокрЛП(ШтрихкодаСтрока);
	
	СтруктураОтвета = ОбменДаннымиКлиентСервер.ПолучитьОтвет(СоединениеИПараметры, АдресРесурсаСПараметрами, "GET");
	
	Если СтруктураОтвета = Неопределено Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	Если СтруктураОтвета.error Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка в запросе: " + СтруктураОтвета.text;
		Сообщение.Сообщить();
	
		Возврат Неопределено;
		
	КонецЕсли;
	
	Возврат СтруктураОтвета;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьШтрихкод(Штрихкод)
	
	ЗаписатьСканированиеШтрихкода(Штрихкод);
	
	СтруктураОтвета = ПолучитьДанныеПоШтрихкоду(Штрихкод);
	Если СтруктураОтвета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОтвета.data = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураОтвета.Свойство("type") Тогда
		
		СтруктураОтвета = ОбработатьРезультат(СтруктураОтвета);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураОтвета.data); 
		
		ИнфоПоОстаткам.Очистить();
		Для Каждого ТекЗапись Из СтруктураОтвета.remnant Цикл
			
			НовСтрока = ИнфоПоОстаткам.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, ТекЗапись);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОсновной(Команда)
	УстановитьЯчейкуДляТовара(Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДополнительной(Команда)
	УстановитьЯчейкуДляТовара(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЯчейкуДляТовара(Основная)
	
	Если Не ЗначениеЗаполнено(Номенклатура)
		Или Не ЗначениеЗаполнено(Ячейка) Тогда
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = "Нужно выбрать товар и ячейку";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	АдресРесурсаСПараметрами = СоединениеИПараметры.Параметры.АдресРесурса + "/setGoodsCell?"
		+ "goods=" + Номенклатура.УникальныйИдентификатор()
		+ "&cell=" + Ячейка.УникальныйИдентификатор()
		+ "&main=" + ?(Основная = Истина, "true", "false");
	
	Попытка
		ОбменДаннымиКлиентСервер.ПолучитьОтвет(СоединениеИПараметры, АдресРесурсаСПараметрами, "POST",, Ложь);
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = СтрШаблон("Для товара %1 установлена %2 ячейка %3 ",
			Номенклатура,
			Формат(Основная, "БЛ=Дополнительная; БИ=Основная;"),
			Ячейка);
		СообщениеПользователю.Сообщить();
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Попытка
		ТекущийПользователь = ПараметрыСеанса.ТекущийПользователь;
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкиСоединения()
	
	Возврат ОбменДаннымиСервер.ПолучитьНастройкиСоединения();	//ПользователиИнформационнойБазы.ТекущийПользователь()
	
КонецФункции

&НаСервере
Функция ОбработатьРезультат(СтруктураОтвета)
	
	СтруктураРезультат = Новый Структура("data, remnant", Новый Структура, Новый Массив);
	
	Если СтруктураОтвета.data.Свойство("Номенклатура") Тогда
		СтруктураРезультат.data.Вставить("Номенклатура", ОбменДаннымиСервер.ПолучитьСоздатьОбновитьСсылку(СтруктураОтвета.data.Номенклатура, "Номенклатура", Ложь));	
	КонецЕсли;
	Если СтруктураОтвета.data.Свойство("Упаковка") Тогда
		СтруктураРезультат.data.Вставить("Упаковка", ОбменДаннымиСервер.ПолучитьСоздатьОбновитьСсылку(СтруктураОтвета.data.Упаковка, "Упаковки", Ложь));	
	КонецЕсли;
	Если СтруктураОтвета.data.Свойство("Ячейка") Тогда
		СтруктураРезультат.data.Вставить("Ячейка", ОбменДаннымиСервер.ПолучитьСоздатьОбновитьСсылку(СтруктураОтвета.data.Ячейка, "Ячейки", Ложь));	
	КонецЕсли;
	
	Для Каждого ТекСтрока Из СтруктураОтвета.remnant Цикл
		СтруктураСтроки = Новый Структура("Номенклатура, Ячейка, Упаковка, ВНаличииОстаток, ВНаличииОстатокШтуки");
		СтруктураСтроки.Номенклатура = ОбменДаннымиСервер.ПолучитьСоздатьОбновитьСсылку(ТекСтрока.Номенклатура, "Номенклатура", Ложь);
		СтруктураСтроки.Ячейка = ОбменДаннымиСервер.ПолучитьСоздатьОбновитьСсылку(ТекСтрока.Ячейка, "Ячейки", Ложь);
		СтруктураСтроки.Упаковка = ОбменДаннымиСервер.ПолучитьСоздатьОбновитьСсылку(ТекСтрока.Упаковка, "Упаковки", Ложь);
		СтруктураСтроки.ВНаличииОстаток = ТекСтрока.ВНаличииОстаток;
		СтруктураСтроки.ВНаличииОстатокШтуки = ТекСтрока.ВНаличииОстаток * ?(СтруктураСтроки.Упаковка.Знаменатель = 0, 1, СтруктураСтроки.Упаковка.Числитель/СтруктураСтроки.Упаковка.Знаменатель);
		
		Если ТекСтрока.Свойство("ВидЯчейки") Тогда  
			СтруктураСтроки.Вставить("ВидЯчейки", ТекСтрока.ВидЯчейки);
		КонецЕсли;
		
		СтруктураРезультат.remnant.Добавить(СтруктураСтроки);
		
	КонецЦикла;
	
	Возврат СтруктураРезультат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьСканированиеШтрихкода(Штрихкод)
	
	РегистрыСведений.Сканирования.ЗарегистрироватьВводШтрихкода(Штрихкод);
	
КонецПроцедуры

#КонецОбласти
