Функция ПолучитьДанныеПоШтрихкоду(Штрихкод) Экспорт
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|ГДЕ
	|	ШтрихкодыНоменклатуры.Штрихкод В(&Штрихкод)";
	
	Запрос.УстановитьПараметр("Штрихкод", Штрихкод);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекДанные = Новый Структура("Штрихкод, Номенклатура, Упаковка");
		ЗаполнитьЗначенияСвойств(ТекДанные, Выборка);
	Иначе
		ТекДанные = Неопределено;	
	КонецЕсли;
	
	Возврат ТекДанные;
КонецФункции

Функция ПолучитьШтрихкодыПоНоменклатуре(ТЗНоменклатураУпаковки, ВсеУпаковки = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	 
    Если ТипЗнч(ТЗНоменклатураУпаковки) = Тип("ТаблицаЗначений") Тогда		
		Запрос.УстановитьПараметр("ТаблицаИсходная", ТЗНоменклатураУпаковки);

		Запрос.Текст =	"ВЫБРАТЬ
		              	|	ТаблицаИсходная.Номенклатура КАК Номенклатура,
		              	|	ТаблицаИсходная.Упаковка КАК Упаковка
		              	|ПОМЕСТИТЬ втИсходная
		              	|ИЗ
		              	|	&ТаблицаИсходная КАК ТаблицаИсходная";
	Иначе 
		Запрос.Текст =	"ВЫБРАТЬ
		              	|	&Номенклатура КАК Номенклатура,
		              	|	&Упаковка КАК Упаковка
		              	|ПОМЕСТИТЬ втИсходная";
		
		Запрос.УстановитьПараметр("Номенклатура", ТЗНоменклатураУпаковки.Номенклатура);
		Запрос.УстановитьПараметр("Упаковка", ТЗНоменклатураУпаковки.Упаковка);
	КонецЕсли;
	
	Запрос.Текст =	Запрос.Текст +	"
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	Если ВсеУпаковки Тогда
		Запрос.Текст =	Запрос.Текст + "ВЫБРАТЬ
	              	              	|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
	              	              	|	втИсходная.Упаковка КАК УпаковкаНоменклатуры,
	              	              	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	              	              	|	ШтрихкодыНоменклатуры.Упаковка КАК Упаковка
	              	              	|ИЗ
	              	              	|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	              	              	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втИсходная КАК втИсходная
	              	              	|		ПО ШтрихкодыНоменклатуры.Номенклатура = втИсходная.Номенклатура
	              	              	|			И ШтрихкодыНоменклатуры.Упаковка = втИсходная.Упаковка
	              	              	|
	              	              	|ОБЪЕДИНИТЬ
	              	              	|
	              	              	|ВЫБРАТЬ
	              	              	|	ШтрихкодыНоменклатуры.Номенклатура,
	              	              	|	втИсходная.Упаковка,
	              	              	|	ШтрихкодыНоменклатуры.Штрихкод,
	              	              	|	ШтрихкодыНоменклатуры.Упаковка
	              	              	|ИЗ
	              	              	|	втИсходная КАК втИсходная
	              	              	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	              	              	|		ПО втИсходная.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	              	              	|ГДЕ
	              	              	|	ШтрихкодыНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.Упаковки.ПустаяСсылка)";
	Иначе 
		Запрос.Текст =	Запрос.Текст + "ВЫБРАТЬ
	              	              	|	ШтрихкодыНоменклатуры.Номенклатура,
	              	              	|	втИсходная.Упаковка,
	              	              	|	ШтрихкодыНоменклатуры.Штрихкод,
	              	              	|	ШтрихкодыНоменклатуры.Упаковка
	              	              	|ИЗ
	              	              	|	втИсходная КАК втИсходная
	              	              	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	              	              	|		ПО втИсходная.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура";	
		
	КонецЕсли;	
	
	Возврат Запрос.Выполнить().Выгрузить();	
КонецФункции

Процедура ДобавитьШтрихкодыВТаблицу(ТаблицаТовары, ИдентификаторСтроки) Экспорт
		
	Если ИдентификаторСтроки = Неопределено Тогда
		ТЗШтрихкоды = ПолучитьШтрихкодыПоНоменклатуре(ТаблицаТовары.Выгрузить(, "Номенклатура, Упаковка")); 
		Для Каждого ТекСтрокаТЧ Из ТаблицаТовары Цикл
			ДобавитьШтрихкодыВСписок(ТекСтрокаТЧ, ТЗШтрихкоды.НайтиСтроки(Новый Структура("Номенклатура, Упаковка", ТекСтрокаТЧ.Номенклатура, ТекСтрокаТЧ.Упаковка)));	
		КонецЦикла;
	Иначе 
		ТекСтрокаТЧ = ТаблицаТовары.НайтиПоИдентификатору(ИдентификаторСтроки);
		ТЗШтрихкоды = ПолучитьШтрихкодыПоНоменклатуре(ТекСтрокаТЧ); 
		ДобавитьШтрихкодыВСписок(ТекСтрокаТЧ, ТЗШтрихкоды);		
	КонецЕсли;
			
КонецПроцедуры

Процедура ДобавитьШтрихкодыВСписок(СтрокаТЧ, Штрихкоды) Экспорт
	
	СтрокаТЧ.Штрихкоды.Очистить();
	Для Каждого ТекШтрихкод Из Штрихкоды Цикл
		СтрокаТЧ.Штрихкоды.Добавить(ТекШтрихкод.Штрихкод, Прав(ТекШтрихкод.Штрихкод, 4));		
	КонецЦикла;
		
КонецПроцедуры